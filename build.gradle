plugins {
	id 'fabric-loom' version '0.8-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = "EntityUtils"
		url = "https://dl.cloudsmith.io/public/louwinds-mods/entity-utils/maven"
	}
	maven {
		name = "Geckolib"
		url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven"
	}
	maven {
		name = "Reload"
		url = "https://dl.cloudsmith.io/public/louwinds-mods/reload/maven"
	}
	maven {
		name = "Worldgen-API"
		url = "https://dl.cloudsmith.io/public/louwinds-mods/worldgen-api/maven"
	}
}

dependencies {
	implementation 'org.jetbrains:annotations:20.1.0'

	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modImplementation "github.louwind.entity-utils:EntityUtils:${project.entityutils_version}"
	modImplementation "github.louwind.reload-api:Reload:${project.reload_version}"
	modImplementation "github.louwind.worldgen-api:Worldgen-API:${project.worldgen_version}"

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "software.bernie.geckolib:geckolib-fabric-1.17:${project.geckolib_version}"
}

minecraft {
	accessWidener = file("src/main/resources/deep_trenches.accesswidener")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 16
}

task datapack(type: Zip) {
	from 'src/main/resources/data'
	include '**/*'
	archiveName "${project.archivesBaseName}-${project.mod_version}.zip"
	destinationDir(file('/build/libs'))
}

task apiJar(type: Jar) {
	from sourceSets.main.output
	exclude 'data'
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

runClient.configure {
	dependsOn datapack
}

jar.configure {
	dependsOn datapack
	dependsOn apiJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}
	repositories {

	}
}